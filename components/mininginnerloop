//At a node
clearsysmsg
say '[coord'
wait 610
say 'all follow me'
wait 610
say 'all guard me'
wait 1000

//Start the mining process with a 1 time mining action
if rhandempty ?? 0
	dclicktype '3718' 'backpack'
	wait 600
endif
dclick hands

//Start the permanent loop.
while not dead

    //Make sure a pickaxe is equipped
    if rhandempty ?? 0
        dclicktype '3718' 'backpack'
        wait 600
    endif
    
    //Now begin monitoring for results:
    //you dig some = succesfully mined`
    //you losen some = succesfully mined
    //no longer hindered = you have completed a rope action
    if insysmsg 'you dig some' or insysmsg 'you loosen some' or insysmsg 'no longer hindered'
        dclick hands
    endif
	
	//Break out of the permanent loop if there is nothing left to gather at this node.
	if insysmsg 'you do not see any harvestable resources nearby'
        overhead 'No ore!' 55
        break
    endif

    //Check Tracking for murderers and check if we have a tool
    if insysmsg 'tracking' or not findtype '3718' 'self'
	    overhead 'PK Alert or no tools' 33
        script 'gtfo'
    endif
	
    //Pause when party menu is up
    while gumpexists 3527489586
        overhead 'pause'
        say '[coord'
        while not dead
            if insysmsg 'tracking'
                script 'gtfo'
            endif
            if not gumpexists 3527489586
                if rhandempty ?? 0
                    dclicktype 'pickaxe' 'backpack'
                    wait 600
                endif
                overhead 'resume'
                dclick hands
                break
            endif
        endwhile 
    endwhile
    
    //Hand ore to packhorses if overweight
    if diffweight < 15
        hotkey 'Next Friend Target'
        wait 500
        organizer 6 'set'
        waitfortarget 1000
        lasttarget
        wait 1000
        organizer 6
        //Build a shortcircuitable buffer of time for organizer to move goods to llama.
        for 10
            if not queued
                break
            endif
            overhead 'organizing...' 40
            wait 615
        endfor
        dclick hands
    endif
    
    //Wait 20 milliseconds just to chill
    wait 20
endwhile